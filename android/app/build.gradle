plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

// --- ここから追加：key.propertiesの読み込み ---
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
// --- ここまで追加 ---

// dart-define を入れる変数を宣言しています。
def dartDefines = [:];
if (project.hasProperty('dart-defines')) {
    dartDefines = dartDefines + project.property('dart-defines')
        .split(',')
        .collectEntries { entry ->
            def pair = new String(entry.decodeBase64(), 'UTF-8').split('=')
            pair.length == 2 ? [(pair.first()): pair.last()] : [:]
        }
}

android {
    namespace = "com.example.flutter_engineer_codecheck"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    // --- ここから追加：SigningConfigs (buildTypesより上に書く) ---
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    // --- ここまで追加 ---

    defaultConfig {
        // applicationId dartDefines.appId ?: "com.example.flutter_engineer_codecheck"
        applicationId dartDefines.appId ?: "com.search.flutter.app.prod"
        resValue "string", "app_name", dartDefines.appName ?: "flutter_engineer_codecheck"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    buildTypes {
        release {
            // signingConfig をデバッグ用からリリース用に変更
            signingConfig signingConfigs.release
            
            // ストア用ビルドでは最適化を有効にするのが一般的です
            minifyEnabled false 
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

task copySources(type: Copy) {
    from "src/${dartDefines.flavor}/res"
    into 'src/main/res'
}

tasks.whenTaskAdded { task ->
   if (task.name.contains("prepare")) { // copySourcesが頻繁に走りすぎるのを防ぐための調整
       task.dependsOn copySources
   }
}

flutter {
    source = "../.."
}